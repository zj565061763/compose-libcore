plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.android"
def libArtifactId = "compose-libcore"
def libVersionName = "1.0.0-alpha01"

android {
    namespace 'com.sd.lib.compose.libcore'
    compileSdk 33
    defaultConfig {
        minSdk 21
        targetSdk 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions.freeCompilerArgs += ['-module-name', "$libGroupId.$libArtifactId"]
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

dependencies {
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation 'androidx.compose.material3:material3'

    api 'androidx.activity:activity-compose:1.6.1'
    api 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    api 'androidx.lifecycle:lifecycle-process:2.5.1'
    api 'com.github.zj565061763:coroutine:latest.release'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}